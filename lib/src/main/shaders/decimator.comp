#version 450
#pragma shader_stage(compute)

precision highp float;

layout (std430) buffer;
layout (local_size_x_id = 0) in;

layout (set = 0, binding = 0) readonly buffer Params { float phi; float omega; int shifterOffset; int offset[16]; };
layout (set = 0, binding = 1) readonly buffer Taps { float taps[]; };
layout (set = 0, binding = 2) readonly buffer Input { float inBuffer[]; };
layout (set = 0, binding = 3) writeonly buffer Output { float outBuffer[]; };
layout (push_constant) uniform PushConstants { int index; };

void main() {
    int i = int(gl_GlobalInvocationID.x) * 2;

    int middle = taps.length() / 2;

    float re = inBuffer[2 * (i + middle) + 0] * taps[middle];
    float im = inBuffer[2 * (i + middle) + 1] * taps[middle];

    int j = 1;

    while (j < middle) {
        re += inBuffer[2 * (i + middle + j) + 0] * taps[middle + j] +
              inBuffer[2 * (i + middle - j) + 0] * taps[middle - j];
        im += inBuffer[2 * (i + middle + j) + 1] * taps[middle + j] +
              inBuffer[2 * (i + middle - j) + 1] * taps[middle - j];

        j += 2;
    }

    outBuffer[i + 2 * offset[index] + 0] = re;
    outBuffer[i + 2 * offset[index] + 1] = im;
}
